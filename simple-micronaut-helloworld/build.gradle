plugins {
    id "java"
    id "jacoco"
    id "application"

    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "io.micronaut.minimal.application" version "4.0.2"
}

repositories {
    mavenLocal()
    mavenCentral()
}

mainClassName = "io.goodforgod.aws.lambda.simple.micronaut.MicronautInputLambdaEntrypoint"

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

micronaut {
    version("4.0.2")
    processing {
        incremental(true)
        module(project.name)
        group(project.group)
        annotations("io.goodforgod.simplelambda.micronaut.*")
    }
}

configurations.configureEach {
    exclude group: "org.slf4j", module: "slf4j-simple"
}

dependencies {
    annotationProcessor "io.goodforgod:graalvm-hint-processor:1.1.0"
    annotationProcessor "io.micronaut:micronaut-inject-java"
    annotationProcessor "io.micronaut:micronaut-graal"

    compileOnly "io.goodforgod:graalvm-hint-annotations:1.1.0"
    compileOnly "io.micronaut:micronaut-inject-java"
    compileOnly "org.graalvm.nativeimage:svm"
    runtimeOnly "org.yaml:snakeyaml"

    implementation "io.goodforgod:simple-awslambda-micronaut:1.0.0-SNAPSHOT"
    implementation "io.goodforgod:slf4j-simple-logger:2.0.0"

    testAnnotationProcessor "io.micronaut:micronaut-inject-java"
    testImplementation "io.micronaut:micronaut-inject-java"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "org.junit.jupiter:junit-jupiter-params"
}

test {
    useJUnitPlatform()
    testLogging {
        events("passed", "skipped", "failed")
        exceptionFormat("full")
    }

    reports {
        html.required = false
        junitXml.required = false
    }
}

shadowJar {
    mergeServiceFiles()
    manifest {
        attributes "Main-Class": mainClassName
        attributes "Implementation-Version": artifactVersion
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.incremental = true
    options.fork = true
}

javadoc {
    options.encoding = "UTF-8"
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
}

check.dependsOn jacocoTestReport
jacocoTestReport {
    reports {
        xml.required = true
        html.destination file("${buildDir}/jacocoHtml")
    }
}
